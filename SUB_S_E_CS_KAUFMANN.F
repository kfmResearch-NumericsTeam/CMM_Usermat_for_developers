!-----------------------------------------------------------------------
!     SUBROUTINE ZUR BERECHNUNG DER BETONHAUPTDRUCKSPANNUNGEN NACH
!     KAUFMANN (CS = CONCRETE SOFTENING)
!
!     PROGRAMMIERUNG:     DR. KAREL THOMA, HOCHSCHULE LUZERN
!                         PATRICK ROOS, HOCHSCHULE LUZERN
!     DATUM:              02.06.2014
!-----------------------------------------------------------------------
      SUBROUTINE SUB_S_E_CS_KAUFMANN(BETON,E1,E3,FCC,ECU,K_E,SIGMA_C3)

      IMPLICIT NONE

      ! VARIABLENDEKLARATION
      INTEGER, DIMENSION(2), INTENT(IN) :: BETON
      DOUBLE COMPLEX, INTENT(IN) :: E1, E3, FCC, ECU, K_E
      DOUBLE COMPLEX, INTENT(OUT):: SIGMA_C3
      DOUBLE COMPLEX :: FC, K, VAL, ECU_95, DIFF

      ! INTERFACES ZU EXTERNEN FUNKTIONEN
      INTERFACE
      FUNCTION ABS_C(VAL)
        DOUBLE COMPLEX :: ABS_C
        DOUBLE COMPLEX, INTENT(IN) :: VAL
      END FUNCTION ABS_C

      FUNCTION MIN_RC(VAL1,VAL2)
        DOUBLE COMPLEX :: MIN_RC
        DOUBLE COMPLEX, INTENT(IN) :: VAL1, VAL2
      END FUNCTION MIN_RC
      END INTERFACE

      ! VERFESTIGENDES WERKSTOFFGESETZ
      K = CMPLX(0.95D0,0D0,KIND=8)
      ECU_95 = K*ECU

      ! BERECHNUNG DER BETONHAUPTDRUCKSPANNUNG
      IF (REAL(E3) .GT. 0D0) THEN
        SIGMA_C3 = CMPLX(0D0,0D0,KIND=8)

      ELSE IF ((REAL(E3) .LT. REAL(ECU)) .AND. ((2D0-BETON(2))
     &        .LT. 1.1D0)) THEN
        SIGMA_C3 = CMPLX(0D0,0D0,KIND=8)

      ELSE IF (REAL(E3) .LE. 0D0 .AND. REAL(E3) .GE. REAL(ECU_95)) THEN
        VAL = (FCC*FCC)**CMPLX(1D0/3D0,0D0,KIND=8)/
     &        (CMPLX(0.4D0,0D0,KIND=8)+CMPLX(30D0,0D0,KIND=8)*E1)
        VAL = ABS_C(VAL)
        FC = MIN_RC(FCC,VAL)
        SIGMA_C3 = FC*((E3*E3)+(CMPLX(2D0,0D0,KIND=8)*E3*ABS_C(ECU)))/
     &             (ECU*ECU)
      ELSE
        VAL = (FCC*FCC)**CMPLX(1D0/3D0,0D0,KIND=8)/
     &        (CMPLX(0.4D0,0D0,KIND=8)+CMPLX(30D0,0D0,KIND=8)*E1)
        VAL = ABS_C(VAL)
        FC = MIN_RC(FCC,VAL)
        DIFF = -FC*((2*ECU_95)+(CMPLX(2D0,0D0,KIND=8)*ABS_C(ECU)))/
     &         (ECU*ECU)
        SIGMA_C3 = FC*((ECU_95*ECU_95)+CMPLX(2D0,0D0,KIND=8)*ECU_95*
     &             ABS_C(ECU))/(ECU*ECU)+DIFF*(ECU_95-E3)
      END IF

      END SUBROUTINE SUB_S_E_CS_KAUFMANN